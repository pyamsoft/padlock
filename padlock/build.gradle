/*
 * Copyright 2016 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

tasks.whenTaskAdded { task ->
  if (task.name.startsWith("lint")) {
    task.enabled = false
  }
}

apply plugin: "com.android.application"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "net.ltgt.errorprone"
apply plugin: "me.tatarka.retrolambda"

//noinspection GroovyMissingReturnStatement
android {
  compileSdkVersion rootProject.ext.maxSdk
  buildToolsVersion "$buildTools"

  defaultConfig {
    applicationId "com.pyamsoft.padlock"
    minSdkVersion rootProject.ext.minSdk
    targetSdkVersion rootProject.ext.maxSdk
    versionCode 73
    versionName "2.2.0pre1"

    // For vector drawables
    vectorDrawables.useSupportLibrary = true
  }

  dataBinding {
    enabled true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    release {
      //noinspection GroovyAssignabilityCheck
      storeFile new File(RELEASE_STORE_FILE)

      //noinspection GroovyAssignabilityCheck
      keyAlias RELEASE_KEY_ALIAS

      //noinspection GroovyAssignabilityCheck
      keyPassword RELEASE_KEY_PASSWD

      //noinspection GroovyAssignabilityCheck
      storePassword RELEASE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      //noinspection GroovyAssignabilityCheck
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
          "proguard-rules.pro"
    }
  }

  dexOptions {
    javaMaxHeapSize "$heapSize"
  }
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:${rootProject.ext.retrolambda}"

  // Leak Canary
  debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanary"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"

  // Dagger
  annotationProcessor "com.google.dagger:dagger-compiler:$dagger"

  // Firebase Crash
  compile "com.google.firebase:firebase-crash:$firebase"

  // PYDroid
  compile("com.github.pyamsoft.pydroid:pydroid-ui:$pydroid@aar") {
    transitive = true
  }
  compile("com.github.pyamsoft.pydroid:pydroid-design:$pydroid@aar") {
    transitive = true
  }

  debugCompile project(path: ":padlock-iconloader", configuration: "debug")
  releaseCompile project(path: ":padlock-iconloader", configuration: "release")
  debugCompile project(path: ":padlock-main", configuration: "debug")
  releaseCompile project(path: ":padlock-main", configuration: "release")
  debugCompile project(path: ":padlock-onboard", configuration: "debug")
  releaseCompile project(path: ":padlock-onboard", configuration: "release")
  debugCompile project(path: ":padlock-lock", configuration: "debug")
  releaseCompile project(path: ":padlock-lock", configuration: "release")
  debugCompile project(path: ":padlock-list", configuration: "debug")
  releaseCompile project(path: ":padlock-list", configuration: "release")
  debugCompile project(path: ":padlock-purge", configuration: "debug")
  releaseCompile project(path: ":padlock-purge", configuration: "release")
  debugCompile project(path: ":padlock-settings", configuration: "debug")
  releaseCompile project(path: ":padlock-settings", configuration: "release")
}

// AdView
apply plugin: "com.google.gms.google-services"
