/*
 * Copyright (C) 2018 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "com.getkeepsafe.dexcount"

//noinspection GroovyMissingReturnStatement
android {
  compileSdkVersion rootProject.ext.maxSdk

  defaultConfig {
    applicationId "com.pyamsoft.padlock"
    minSdkVersion rootProject.ext.minSdk
    targetSdkVersion rootProject.ext.maxSdk
    versionCode 99
    versionName "3.0.3-SNAPSHOT"

    // For vector drawables
    vectorDrawables.useSupportLibrary = true

    resConfigs "en"

    javaCompileOptions {
      annotationProcessorOptions {
        // Fix kapt
        includeCompileClasspath = true
      }
    }
  }

  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }

  dataBinding {
    enabled true
  }

  signingConfigs {
    release {
      //noinspection GroovyAssignabilityCheck
      storeFile new File(RELEASE_STORE_FILE)

      //noinspection GroovyAssignabilityCheck
      keyAlias RELEASE_KEY_ALIAS

      //noinspection GroovyAssignabilityCheck
      keyPassword RELEASE_KEY_PASSWD

      //noinspection GroovyAssignabilityCheck
      storePassword RELEASE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      //noinspection GroovyAssignabilityCheck
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
          "proguard-rules.pro"
    }

    debug {
      versionNameSuffix "-dev"
    }
  }
}

dependencies {

  // Databinding
  kapt "androidx.databinding:databinding-compiler:$plugin_version"

  // FastAdapter
  implementation "com.mikepenz:fastadapter:$fastAdapter@aar"
  implementation "com.mikepenz:fastadapter-commons:$fastAdapter@aar"

  // Leak Canary
  debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakCanary"
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"
  testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"

  // Dagger
  kapt "com.google.dagger:dagger-compiler:$dagger"

  // Pattern Lock
  implementation "com.andrognito.patternlockview:patternlockview:$patternLock"

  // PYDroid
  implementation "com.pyamsoft.pydroid:ui:$pydroid"

  implementation project(":padlock-base")
  implementation project(":padlock-list")
  implementation project(":padlock-lock")
  implementation project(":padlock-pin")
  implementation project(":padlock-purge")
  implementation project(":padlock-service")
  implementation project(":padlock-settings")
}
